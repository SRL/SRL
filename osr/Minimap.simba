const
  DIR_NORTH = 0.00;
  DIR_EAST  = 90.0;
  DIR_SOUTH = 180.0;
  DIR_WEST  = 270.0;

type
  EMinimapOrb = (MMOrbHP, MMOrbPrayer, MMOrbRun);
  EMinimapDotType = (MMDotPlayer, MMDotItem, MMDotNPC);

type
  TMMOrbInfo = record
    TextBox: TBox;
    Middle: TPoint;
    Radius: Byte;
 end;

type
  TMMCompassInfo = record
    Middle: TPoint;
    Bounds: TBox;
    Radius: Byte;
 end;

type
  TMinimap = record (TInterfaceBase)
    CompassInfo: TMMCompassInfo;
    OrbInfo: array [0..High(EMinimapOrb)] of TMMOrbInfo;
    MaskBMP: Integer;
    MaskTPA: TPointArray;
 end;

var
  Minimap: TMinimap;

procedure TMinimap.__Free();
begin
  if (BitmapExists(Self.MaskBMP)) then
    FreeBitmap(Self.MaskBMP);
end;

procedure TMinimap.__Setup();
begin
  with (Self) do
  begin
    SetName('Minimap');
    SetStatic(True);
    SetBounds([570, 9, 716, 157]);

    with (CompassInfo) do
    begin
      Bounds := [545, 4, 576, 36];
      Middle := [561, 20];
      Radius := 15;
    end;

    with (OrbInfo[Ord(MMOrbHP)]) do
    begin
      Middle := [555, 57];
      Radius := 13;
      TextBox := [520, 55, 539, 68];
    end;

    with (OrbInfo[Ord(MMOrbPrayer)]) do
    begin
      Middle := [557, 101];
      Radius := 11;
      TextBox := [521, 99, 540, 112];
    end;

    with (OrbInfo[Ord(MMOrbRun)]) do
    begin
      Middle := [581, 136];
      Radius := 11;
      TextBox := [545, 135, 564, 148];
    end;

    AddOnTerminate(@Self.__Free);

    MaskBMP := BitmapFromString(147, 149, 'meJzt1cGOhCAUBdH+/5+emZXp' +
              'jIqIwnsFdVPbTlpO0M9nnv0UF/3vll6Z5smin2zO9fNSsMdGein4f' +
              'A+O+jQF++0hTXMKtq360LqnYOWSeDULRp9fwHJ6tQlGn+W45fe6Kx' +
              'h9ot1HJFucj062IN9MZOvwzao2t93EZJV80QK3twjZZHyrkdXwRZt' +
              'cb2U1rt3iZJd80T4H86JV2qXiU41opxrRTrInfKohSmKnGtFONaid' +
              'au/aqUZpvJ1qRDs/bV3hOtmpBrVTjQin2ki+TmrC9YZ7xU41qJ1qU' +
              'XaqUXrRTjginGpQO+GIcKoR7Y5+a2Fw9XaqZbNTjVKDnXAZugunWp' +
              '5u2amWKuGgVcL5nsxW5dtStYQJB+0Szvdkzi7flqqlTThowkErwPm' +
              'By1zhM6da5oSDdgbnezJ5NXDhf9L2CQfNDxw04aAJB004aMJBEw6a' +
              'cNCEgyYcNOGgCQdNOGjCQTuD0y5zBTXhMiccNOGgCQdNOGj1cNrla' +
              'b/PbsIl7FJNuJwJB004aMJBEw7aXTjtMrTfIZyXLluVal66VNVfN+' +
              'FSdQvOt2Webql56ZJ097oJl6QGON+WGWpQ89KF13bdhAuvGU47qJp' +
              'wXDjtoGrCceG0g6rt4eQbRvYQ7tAu/DHn63U17bhq2nHVDuG0Q8Bp' +
              'N4bsdTXtuGracdXk45Jph1Yr2ClY6RWlJh+XbJt2RLXvaVdWi/a52' +
              'LK3D3TFzrag3QRq2xbhm4lsW/Gh8JUXffYvbEq7ucm2zXT1pr9o+0' +
              '1gtxrZ97h8y5JtI8Kptg1kJ9m/IexUO1tmO8nKy2mnWs2y2alWvyS' +
              'fPD9qDQu3U615gXaqPVweuOiT4C0DXPQZUDfYTrUXFwUX/dwzbDxc' +
              '9BNPsjGXzuv2+gZ86fy6dVpXO9X6bXe2vdSEe3fCcXdwxB0W/ZRzT' +
              'jX0JONONeiE4061v/0CYW7pQw==');


    SetBitmapName(Self.MaskBMP, 'Minimap mask');
    FindColorsBitmap(Self.MaskBMP, Self.MaskTPA, $000000);
    OffsetTPA(Self.MaskTPA, Point(Self.FBounds.X1, Self.FBounds.Y1));
  end;
end;

function TMinimap._GetOrbValue(const orb: EMinimapOrb): Integer;
const
  IgnoreCols = [4938344, 0];
  Font = 'StatChars07';
var
  ATPA: array [0..1] of TPointArray;
  TextTPA: TPointArray;
  i: Integer;
begin
  for i := 0 to High(IgnoreCols) do
    FindColors(ATPA[i], IgnoreCols[i], OrbInfo[Ord(orb)].TextBox);

  TextTPA := ReturnPointsNotInTPA(MergeATPA(ATPA), OrbInfo[Ord(orb)].TextBox);
  Result := StrToIntDef(srl.GetTextFromTPA(TextTPA, 1, 3, 100, Font), 0);
end;

function TMinimap.GetPrayerLevel(): Integer;
begin
  Result := Self._GetOrbValue(MMOrbPrayer);
end;

function TMinimap.GetHPLevel(): Integer;
begin
  Result := Self._GetOrbValue(MMOrbHP);
end;

function TMinimap.GetHPPercent(): Byte;
var
  tb, b: TBox;
  TPA: TPointArray;
begin
  with Minimap.OrbInfo[ord(MMOrbHP)] do
    b := [Middle.X - Radius, Middle.Y - Radius, Middle.X + Radius, Middle.Y + Radius];
  FindColorsTolerance(TPA, 1250067, b, 20);
  tb := GetTPABounds(TPA);
  Result := 100 - Ceil((tb.y2 - tb.y1) / (b.y2 - b.y1) * 100);
end;

function TMinimap.GetRunEnergy(): Integer;
begin
  Result := Self._GetOrbValue(MMOrbRun);
end;

function TMinimap.isRunEnabled(const Time: UInt32 = 0): Boolean;
const
  Tol = 15;
  MinCount = 10;
var
  Col: Integer;
  b: TBox;
begin
  if (GetColor(Minimap.OrbInfo[Ord(MMOrbRun)].Middle) = 464438) then
    Col := 105407
  else
    Col := 6806252;

  with (Self.OrbInfo[Ord(MMOrbRun)].Middle) do
    b := [X - 7, Y - 7, X + 7, Y + 7];

  if (Time = 0) then
    Result := (CountColorTolerance(Col, b, Tol) > MinCount)
  else
    Result := srl.WaitMinColorCount(Col, Tol, b, MinCount, Time);
end;

function TMinimap.ToggleRun(const Enable: Boolean): Boolean;
begin
  Result := True;

  with (Self.OrbInfo[Ord(MMOrbRun)]) do
  begin
    if (Enable) and (not Self.isRunEnabled()) then
    begin
      Mouse.Click(Middle, Radius, mouse_Left);
      Result := Self.isRunEnabled(Random(600, 1000));
    end
    else
    if (not Enable) and (Self.isRunEnabled()) then
    begin
      Mouse.Click(Middle, Radius, mouse_Left);
      Result := not Self.isRunEnabled(Random(250, 500));
    end;
  end;
end;

function TMinimap.FindFlag(out p: TPoint): Boolean;
const
  PoleColor = 1127261;
  FlagColor = 255;
  LightPoleColor = 284358;
var
  TPA, TPAPoles: TPointArray;
  ATPA, ATPAColumns: T2DPointArray;
  i, j, Img: Integer;
begin
  p := [-1, -1];
  Img := BitmapFromClient(Self.getBounds());
  SetBitmapName(Img, 'Find flag bitmap');

  try
    if (not FindColorsBitmap(Img, TPA, PoleColor)) then
      Exit(False);

    with GetMufasaBitmap(Img) do
      for i := 0 to High(TPA) do
          if (GetPixel(TPA[i].x - 1, TPA[i].y) = FlagColor) or (GetPixel(TPA[i].x - 1, TPA[i].y) = LightPoleColor) and
             (GetPixel(TPA[i].X, TPA[i].y - 1) = PoleColor) or (GetPixel(TPA[i].x, TPA[i].y + 1) = PoleColor) then
                TPAPoles += TPA[i];

    if (Length(TPAPoles) = 0) then
      Exit(False);

    ATPAColumns := FindTPAColumns(TPAPoles);
    for i := 0 to High(ATPAColumns) do
    begin
      ATPA := SplitTPA(ATPAColumns[i], 1);

      for j := 0 to High(ATPA) do
        if (InRange(GetTPABounds(ATPA[j]).Height, 4, 20)) then
        begin
          Result := True;
          p := ATPA[j][0];

          while (p.y < (Self.getBounds().Height - 1)) and (FastGetPixel(Img, p.x, p.y) = PoleColor) do
            Inc(p.y);

          if (FastGetPixel(Img, p.x, p.y) <> PoleColor) then // Make sure we have the bottom of the pole
            Dec(p.y);
        end;
    end;
  finally
    FreeBitmap(Img);
  end;

  Inc(p.x, Self.GetBounds().X1);
  Inc(p.y, Self.GetBounds().Y1);
end;

function TMinimap.isFlagPresent(const WaitTime: UInt32 = 0): Boolean;
var
  TimeOut: UInt64;
  p: TPoint;
begin
  Result := False;
  TimeOut := GetTickCount64() + WaitTime;

  repeat
    if (Self.FindFlag(p)) then
      Exit(True);

    Wait(Random(50, 200));
  until (GetTickCount64() > TimeOut);
end;

procedure TMinimap.WaitFlag(const Dist: UInt32 = 0; ExitIfNotMoving: Boolean = True);
var
  TimeOut: UInt64;
  d, LastDist, SameDists: UInt32;
  p: TPoint;
begin
  TimeOut := GetTickCount64() + Random(23000, 25000);
  LastDist := 500;
  SameDists := 0;

  while (TimeOut > GetTickCount64()) and (Self.FindFlag(p)) do
  begin
    d := Distance(p, Self.getMiddle());
    if (d <= Dist) then
      Exit();

    if (d = LastDist) then
      Inc(SameDists)
    else
      SameDists := 0;

    if (ExitIfNotMoving) and (SameDists >= 20) then
      Exit();

    LastDist := d;
    Wait(Random(50, 100));
  end;
end;

(*
Minimap.WaitFlagEx
~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TMinimap.WaitFlagEx(MaxDist: Byte = 5): Boolean;

Extra WaitFlag method that Waits for the flag to disapear but will return if the
position we walked to is where the flag was first clicked.

  - Useful to know if you were able to walk into a room or not (blocked entrance, etc).
  - MaxDist param is default, but is the max distance the flag can move else we result False.

Example:

.. code-block:: pascal

    if (Minimap.WaitFlagEx()) then
      srl.Writeln('We have walked to the position succesfully')
    else
      srl.Writeln('We have finished walking, but the flag moved alot so we didnt hit our target pos');
*)
function TMinimap.WaitFlagEx(MaxDist: Byte = 5): Boolean;
var
  OldPt, NewPt: TPoint;
  t: UInt64;
  Found: Boolean;
begin
  Result := True;

  t := GetTickCount64() + Random(2500, 3500);
  while (not (Found := Self.FindFlag(OldPt))) and (t > GetTickCount64()) do
    Wait(Random(25, 50));

  if (not Found) then
    Exit(False);

  t := GetTickCount64() + Random(22000, 24000);
  while (FindFlag(NewPt)) and (t > GetTickCount64()) do
  begin
    if (Distance(OldPt, NewPt) > MaxDist) then
    begin
      Self.WaitFlag();
      Exit(False);
    end;

    OldPt := NewPt;
    Wait(Random(25, 50));
  end;
end;

function TMinimap.GetCompassAngle(AsDegrees:Boolean=True): Extended;
var
  north,south: TPoint;
  M:TPoint := Self.CompassInfo.Middle;
  TPA:TPointArray;
begin
  if (not FindColorsTolerance(TPA, 1911089, Self.CompassInfo.Bounds, 25)) then
    Exit(srl.Writeln('Minimap.GetCompassAngle: Failed to find the `N` in the compass', 0.00));

  FilterPointsDist(TPA, 8, 15, M.x, M.y);
  if (Length(TPA) = 0) then
    Exit(srl.Writeln('Minimap.GetCompassAngle: Failed to find the `N` in the compass (after filter)', 0.00));
  north := MiddleTPA(SplitTPA(TPA,3)[0]);
  south := RotatePoint(north, PI, M.x, M.y);
  if (not FindColorsTolerance(TPA, 920735, Self.CompassInfo.Bounds, 25)) then
    Exit(srl.Writeln('Minimap.GetCompassAngle: Failed to find red compass dots color', 0.00));
  
  FilterPointsDist(TPA, 0,6, south.x,south.y);
  SortTPAFrom(TPA, M);
  if (Length(TPA) > 0) then
    Result := FixRad(ArcTan2(TPA[high(tpa)].y-M.y, TPA[high(tpa)].x-M.x) - (PI/2))
  else
    srl.Writeln('Minimap.GetCompassAngle: Failed to find compass dial color');

  if (AsDegrees) then
    Result := Degrees(Result);
end;

function TMinimap.MouseSetCompassAngle(Angle, Tolerance: Extended): Boolean;

  procedure MoveRotate(xs, ys, xe, ye, Angle, Tolerance: Extended);
  const
    sqrt2 := sqrt(2);
    sqrt3 := sqrt(3);
    sqrt5 := sqrt(5);
  var
    veloX, veloY, windX, windY, veloMag, dist, randomDist, lastDist, D, compassDist: Extended;
    TargetArea, Wind, Gravity: Extended;
    lastX, lastY, Speed, W, TDist, rnd_xs, rnd_ys, maxDist, rand, rand2: Integer;
    maxStep, rCnc: Extended;
    t: UInt64;
    oldSpeed: Integer;
  begin
    oldSpeed := Mouse.GetSpeed();
    Mouse.SetSpeed(Random(20, 22));

    Speed := Mouse.GetSpeed();
    Gravity := Mouse.GetGravity(mouse_Human);
    Wind := Mouse.GetWind(mouse_Human);
    TargetArea := 10.0 * ((Random(Speed) / 2.0 + Speed) / 10.0);
    rand := Random(25, 40);
    rand2 := Random(7, 12);
    MaxDist := 15;
    TDist := Distance(Round(xs), Round(ys), Round(xe), Round(ye));
    t := GetTickCount64() + Random(7000, 8500);
    Mouse.Hold(mouse_Middle);

    repeat
      if (GetTickCount64() > t) then
        Break;

      dist := hypot(xs - xe, ys - ye);
      wind := minE(wind, dist);
      if (dist < 1) then
        dist := 1;

      D := (Round((Round(TDist)*0.3))/7);
      if (D > maxDist) then
        D := maxDist;
      if (D < 4) then
        D := 4;

      rCnc := random(6);
      if (rCnc = 1) then
        D := Random(2, 3);

      if (D <= Round(dist)) then
        maxStep := D
      else
        maxStep := round(dist);

      if (dist >= targetArea) then
      begin
        windX := windX / sqrt3 + (random(round(wind) * 2 + 1) - wind) / sqrt5;
        windY := windY / sqrt3 + (random(round(wind) * 2 + 1) - wind) / sqrt5;
      end else
      begin
        windX := windX / sqrt2;
        windY := windY / sqrt2;
      end;

      veloX := veloX + windX;
      veloY := veloY + windY;
      veloX := veloX + gravity * (xe - xs) / dist;
      veloY := veloY + gravity * (ye - ys) / dist;

      if (hypot(veloX, veloY) > maxStep) then
      begin
        randomDist := maxStep / 2.0 + random(round(maxStep) div 2);
        veloMag := sqrt(veloX * veloX + veloY * veloY);
        veloX := (veloX / veloMag) * randomDist;
        veloY := (veloY / veloMag) * randomDist;
      end;

      lastX := round(xs);
      lastY := round(ys);
      xs := xs + veloX;
      ys := ys + veloY;
      rnd_xs := Round(xs);
      rnd_ys := Round(ys);

      if (lastX <> rnd_xs) or (lastY <> rnd_ys) then
        Mouse.SetPosition(Point(rnd_xs, rnd_ys));

      W := (Random((round(100/Speed)))*6);
      if (W < 4) then
        W := 4;
      W := Round(W * 0.4);

      Wait(W);
      LastDist := dist;
      CompassDist := Abs(DeltaAngle(Minimap.GetCompassAngle(), Angle));
      if ((CompassDist < Rand) or (CompassDist < Rand2)) then
      begin
        if (CompassDist < Rand) then
          maxDist := Random(5, 8);
        if (CompassDist < Rand2) then
          maxDist := Random(2, 4);
      end else
        maxDist := 15; // Default

      if (CompassDist <= Tolerance) then
        Break;
    until (Hypot(xs - xe, ys - ye) < 1);

    Mouse.Release(mouse_Middle);
    Wait(Random(150, 250));

    Mouse.setSpeed(oldSpeed);
  end;

const
  MIN_Y_DROP = 105;
  MIN_X_MOVE = 130;

  RIGHT_START: TBox = [0, 0, 150, 380];
  RIGHT_END_START_X = [620, 675];

  LEFT_START: TBox = [600, 0, 765, 380];
  LEFT_END_START_X = [0, 140];

var
  CurrentAngle, AngleDistance: Extended;
  CompassDist: Extended;
  mp, p: TPoint;
  moveRight: Boolean;
  t: UInt64;
begin
  Result := False;

  Angle := FixD(Angle);
  CurrentAngle := Self.GetCompassAngle();
  CompassDist := Abs(DeltaAngle(CurrentAngle, Angle));
  if (CompassDist <= 3) then
    Exit(True);

  t := GetTickCount64() + Random(5000, 7000);
  while (CompassDist > Tolerance) and (t > GetTickCount64()) do
  begin
    AngleDistance := CurrentAngle - Angle;
    moveRight := ((AngleDistance > 0) and (Abs(AngleDistance) <= 180)) or ((AngleDistance < 0) and (Abs(AngleDistance) > 180));
    mp := Mouse.GetPosition();

    if (moveRight) then
    begin
      if (not PointInBox(mp, GetClientBounds())) then
        Mouse.Move(RIGHT_START, False, False);

      if ((mp.x + MIN_X_MOVE) < RIGHT_END_START_X[0]) then
        { No need to move mouse! }
      else
        Mouse.Move(RIGHT_START, False, False);

      mp := Mouse.GetPosition();
      p.x := Random(RIGHT_END_START_X[0], RIGHT_END_START_X[1]);
      p.y := Random(mp.y + MIN_Y_DROP, GetClientBounds().Y2);
    end else // moveLeft
    begin
      if (not PointInBox(mp, GetClientBounds())) then
        Mouse.Move(LEFT_START, False, False);

      if ((mp.x - MIN_X_MOVE) > LEFT_END_START_X[1]) then
        { No need to move mouse! }
      else
        Mouse.Move(LEFT_START, False, False);

      mp := Mouse.GetPosition();
      p.x := Random(LEFT_END_START_X[0], LEFT_END_START_X[1]);
      p.y := Random(mp.y + MIN_Y_DROP, GetClientBounds().Y2);
    end;

    MoveRotate(mp.x, mp.y, p.x, p.y, Angle, Tolerance);
    CurrentAngle := Self.GetCompassAngle();
    CompassDist := Abs(DeltaAngle(CurrentAngle, Angle));
  end;

  Result := CompassDist < Tolerance;
end;

function TMinimap.SetCompassAngle(Angle: Extended; UseMouse: Boolean = True; Tolerance: Integer = 5): Boolean;

  function Compass_Distance(Angle, Target: Extended): Extended;
  begin
    if (Abs(Target - Angle) < 180) then
    begin
      Result := Target - Angle;
      Exit;
    end;

    if (Target > Angle) then
      Result := Target - Angle - 360
    else
      Result := Target - Angle + 360;
  end;

  procedure Key(Left: Boolean);
  begin
    case (Left) of
      True:
        begin
          KeyUp(VK_RIGHT);
          KeyDown(VK_LEFT);
        end;
      False:
        begin
          KeyUp(VK_LEFT);
          KeyDown(VK_RIGHT);
        end;
    end;
  end;

var
  TimeOut: UInt64;
  CurrentAngle, AngleDistance: Extended;
  Left: Boolean;
begin
  if (UseMouse) then
    Exit(Self.MouseSetCompassAngle(Angle, Tolerance));

  Angle := FixD(Angle);
  CurrentAngle := Self.GetCompassAngle();
  if (CurrentAngle = Angle) then
    Exit(True);

  TimeOut := GetTickCount64() + Random(9000, 11000);
  while (Abs(Compass_Distance(CurrentAngle, Angle)) > Tolerance) and (GetTickCount64() < TimeOut) do
  begin
    AngleDistance := CurrentAngle - Angle;
    Left := ((AngleDistance > 0) and (Abs(AngleDistance) <= 180)) or ((AngleDistance < 0) and (Abs(AngleDistance) > 180));
    Key(Left);

    CurrentAngle := Self.GetCompassAngle();
    Result := Abs(Compass_Distance(CurrentAngle, Angle)) <= Tolerance;
    if (Result) then
      Break();

    Wait(Random(15, 30));
  end;

  if (GetTickCount64() >= TimeOut) then
    srl.Writeln('Minimap.SetCompassAngle(): Setting angle timed out');

  KeyUp(VK_LEFT);
  KeyUp(VK_RIGHT);

  Wait(Random(270, 420));
end;

function TMinimap.GetDots(DotType: EMinimapDotType; SortFromPlayer: Boolean = True): TPointArray;
const { aligned with EMinimapDotType }
  DOT_COL_DIFF = [1907997, 23, 4369];
  NPC_DOT_DIFF_2 = 1114626;
var
  img, i: Integer;
  TPA: TPointArray;
begin
  img := BitmapFromClient(Self.GetBounds());

  try
    if (not FindColorsBitmap(img, TPA, 65536)) then
      Exit;

    for i := 0 to High(TPA) do
      if (TPA[i].X > 0) and (TPA[i].Y > 0) then
        if (FastGetPixel(img, TPA[i].X-1, TPA[i].Y-1) -
            FastGetPixel(img, TPA[i].X, TPA[i].Y-1) = DOT_COL_DIFF[Ord(DotType)]) then
          Result += Point(TPA[i].X, TPA[i].Y - 2)
        else
        begin
          if (FastGetPixel(img, TPA[i].X, TPA[i].Y-2) -
              FastGetPixel(img, TPA[i].X, TPA[i].Y-1) = DOT_COL_DIFF[Ord(DotType)]) then
            Result += Point(TPA[i].X - 1, TPA[i].Y - 1)
          else if (DotType = MMDotNPC) then
          begin
            if (FastGetPixel(img, TPA[i].X, TPA[i].Y-2) -
              FastGetPixel(img, TPA[i].X, TPA[i].Y-1) = NPC_DOT_DIFF_2) then
            Result += Point(TPA[i].X + 2, TPA[i].Y - 1)
          end;
        end;

    if (Length(Result) > 0) then
    begin
      ClearDoubleTPA(Result);
      OffsetTPA(Result, Point(Self.GetBounds().X1, Self.GetBounds().Y1));
      if (SortFromPlayer) then
        SortTPAFrom(Result, Self.GetMiddle());
    end;
  finally
    FreeBitmap(img);
  end;
end;

function TMinimap.FindDots(DotType: EMinimapDotType; MinCount: UInt32 = 1): Boolean;
begin
  Result := Length(Self.GetDots(DotType, False)) >= MinCount;
end;

function TMinimap.isPointOn(pt: TPoint; Full: Boolean = True): Boolean;
var
  p: TPoint;
begin
  Result := False;

  if (PointInBox(pt, Self.getBounds())) then
  begin
    p := [pt.X - Self.GetBounds().X1, pt.Y - Self.GetBounds().Y1];
    if (GetMufasaBitmap(Self.MaskBMP).PointInBitmap(p.x, p.y)) then
    begin
      if (Full) then
        Result := (FastGetPixel(Self.MaskBMP, p.x, p.y) = $FFFFFF) or
                  (FastGetPixel(Self.MaskBMP, p.x, p.y) = $FF)
      else
        Result := FastGetPixel(Self.MaskBMP, p.x, p.y) = $FF;
    end;
  end;
end;

procedure TMinimap.FilterPoints(var TPA: TPointArray);
begin
  TPA := ClearTPAFromTPA(TPA, Self.MaskTPA);
end;

function TMinimap.FindDTMs(const DTM: Integer; out Matches: TPointArray; Step: Extended = 1.00; Range: UInt32 = 25): Boolean;

  function GetSearchAngles(TheAngle, TheRange: Extended): T2DExtendedArray;
  var
    StartA, EndA: Extended;
  begin
    StartA := (TheAngle - TheRange);
    EndA := (TheAngle + TheRange);

    if (StartA < 0.00) then
    begin
      Result += TExtendedArray([360 + StartA, 360]);
      Result += TExtendedArray([0, 0 + EndA]);
    end else if (EndA > 360.0) then
    begin
      Result += TExtendedArray([StartA, 360]);
      Result += TExtendedArray([0, 0 + Abs(EndA - 360)]);
    end else
      Result += TExtendedArray([StartA, EndA]);
  end;

var
  SearchAngles, FoundAngles, tmp: T2DExtendedArray;
  i: integer;
  TPA: TPointArray;
begin
  SetLength(Matches, 0);
  SearchAngles := GetSearchAngles(Self.GetCompassAngle(), Range);

  for i := 0 to High(SearchAngles) do
    with Self.GetBounds() do
      if (FindDTMsRotatedSE(DTM, TPA, X1, Y1, X2, Y2, SearchAngles[i][0], SearchAngles[i][1], Step, tmp)) then
      begin
        Insert(tmp, FoundAngles, Length(FoundAngles));
        AppendTPA(Matches, TPA);
      end;

  Self.FilterPoints(Matches);
  Result := Length(Matches) > 0;
end;

function TMinimap.GetColorPercent(Color: Integer; Tolerance: Integer = 0): Integer;
var
  TPA: TPointArray;
  Max: Integer;
begin
  FindColorsBitmap(Self.MaskBMP, TPA, $FFFFFF);
  Max := Length(TPA);
  FindColorsTolerance(TPA, Color, Self.GetBounds(), Tolerance);
  Self.FilterPoints(TPA);
  Result := Round((Length(TPA) / Max) * 100);
end;

function TMinimap.isPlayerMoving(MinShift: Integer = 300): Boolean;
var
  m: TPoint;
begin
  m := Self.getMiddle();
  Result := GetPixelShift([m.x - 35, m.y - 35, m.x + 35, m.y + 35], 350) > MinShift;
end;

procedure TMinimap.WaitPlayerMoving(UseFlag: Boolean = True; TimeOut: Integer = 30000; MinShift: Integer = 300);
var
  t: UInt64;
begin
  t := GetTickCount64() + TimeOut;
  while (t > GetTickCount64()) do
  begin
    if (UseFlag) and (Self.isFlagPresent()) then
      Self.WaitFlag;
    if (not Self.isPlayerMoving()) then
      Exit;
    Wait(Random(100, 200));
  end;
end;

function TMinimap.isPrayerEnabled(const Time: UInt32 = 0): Boolean;
var
  b: TBox;
begin
  with (Self.OrbInfo[Ord(MMOrbPrayer)].Middle) do
    b := [X - 7, Y - 7, X + 7, Y + 7];

  if (Time = 0) then
    Result := (CountColorTolerance(15988210, b, 21) > 15)
  else
    Result := srl.WaitMinColorCount(15988210, 21, b, 15, Time);
end;

function TMinimap.TogglePrayer(const Enable: Boolean): Boolean;
begin
  Result := True;

  with (Self.OrbInfo[Ord(MMOrbPrayer)]) do
  begin
    if (Enable) and (not Self.isPrayerEnabled()) then
    begin
      Mouse.Click(Middle, Radius, mouse_Left);
      Result := Self.isRunEnabled(Random(600, 1000));
    end
    else
    if (not Enable) and (Self.isPrayerEnabled()) then
    begin
      Mouse.Click(Middle, Radius, mouse_Left);
      Result := not Self.isRunEnabled(Random(250, 500));
    end;
  end;
end;

begin
  Minimap.__Setup();
end;
