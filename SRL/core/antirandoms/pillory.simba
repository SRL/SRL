(*
Pillory
=======

Stores all the routines to solve the Pillory random.  None of these routines
should be used throughout scripts.  They only need to be called in SRL's random
detection methods.

This solver uses color counting to determine the shape
of the lock and which key to use.

Original Author : DannyRS
Edits Made By   :

*)

procedure PL_Message(Message: string);
begin
  addToSRLLog('[Pillory Random] ' + Message);
end;

function PL_LockScreen: Boolean;
begin
  Result := (LoggedIn and (GetColor(497, 34) = 65536));
end;

function PL_Detect: boolean;
var
  tpa: TPointArray;
begin
  if (not tabExists(TAB_OPTIONS)) and (tabExists(TAB_FRIENDS)) then
  begin
    // red doors of the cages
    FindColorsTolerance(tpa, 235, MMCX - 15, MMCY - 15, MMCX + 15, MMCY + 15, 20);
    Result := (inRange(length(tpa), 3, 18));
  end;
end;

(**
 * Author: DannyRS
 * Last modified 15/03/2013 By DannyRS
 * Description: Opens Cage Interface
 *)
function PL_ClickCage: Boolean;
var
  t: Integer;
begin
  Result := False;

  t := GetSystemTime;

  if (not LoggedIn) then Exit;

  Result := PL_LockScreen;

  if (not Result) then
  begin
    repeat
      MMouse(235, 120, 40, 30);

      Wait(RandomRange(90, 250));

      if (IsUpTextMultiCustom(['Unlock','lock','Cage','age'])) then
      begin
        ClickMouse2(mouse_left);
        if (waitFunc(@PL_LockScreen, 50, 5000)) then
        begin
          PL_Message('[PL_ClickCage] Opened Lock Screen');
          Result := True;
        end else
          PL_Message('[PL_ClickCage] Failed to open Lock Screen');
      end;
    until (Result) or (GetSystemTime - t > 10000);
  end;
end;

(**
 * Author: DannyRS
 * Last modified 15/03/2013 By DannyRS
 * Description: Clicks a key 0-2
 *)
function PL_ClickKey(Slot: Integer): Boolean;
var
  p: TPoint;
begin
  Result := False;

  if (not PL_LockScreen) then Exit;

  p := Point(209 + (Slot*93), 251);

  MMouse(RandomRange(p.x - 2, p.x + 2), RandomRange(p.y - 4, p.y + 4), 0, 0);

  ClickMouse2(mouse_left);

  Result := True;
end;

(**
 * Author: DannyRS
 * Last modified 15/03/2013 By DannyRS
 * Description: Scans for lock type
 *)
function PL_GetLock: String;
var
  CNum,Count,CountTime,WatchTime: Integer;
begin
  Result := '';
  WatchTime := RandomRange(9000,10000);
  setColorToleranceSpeed(2);
  setToleranceSpeed2Modifiers(0.21, 0.92);
  MarkTime(CountTime);
  repeat
    CNum := CountColorTolerance(1319982, 60, 65, 150, 150, 4);
    if (CNum > Count) then
      Count := CNum;
  until (TimeFromMark(CountTime) > WatchTime);
  if (Count > 840) and (Count < 900) then Result := 'square';
  if (Count > 570) and (Count < 720) then Result := 'triangle';
  if (Count > 400) and (Count < 550) then Result := 'diamond';
  if (Count > 950) and (Count < 1200) then Result := 'circle';
  PL_Message('[PL_GetLock] Lock Found: ' + Result + ' [' + ToStr(Count) + ']');
end;

(**
 * Author: DannyRS
 * Last modified 15/03/2013 By DannyRS
 * Description: Scans for key types
 *)
function PL_GetKeys: TStringArray;
var
  WatchTime: Integer;
begin
  SetLength(Result,3);
  SetColorToleranceSpeed(2);
  SetToleranceSpeed2Modifiers(0.21, 0.92);

  MarkTime(WatchTime);
  Result[0] := '';
  repeat
    case CountColorTolerance(1319982, 180, 170, 235, 260, 4) of
      286: Result[0] := 'triangle';
      493: Result[0] := 'diamond';
      506: Result[0] := 'circle';
      729: Result[0] := 'square';
    end;
  until (not (Result[0] = '')) or (TimeFromMark(WatchTime) > 10000);

  MarkTime(WatchTime);
  Result[1] := '';
  repeat
    case CountColorTolerance(1319982, 265, 170, 345, 260, 4) of
      286: Result[1] := 'triangle';
      493: Result[1] := 'diamond';
      506: Result[1] := 'circle';
      729: Result[1] := 'square';
    end;
  until (not (Result[1] = '')) or (TimeFromMark(WatchTime) > 10000);

  MarkTime(WatchTime);
  Result[2] := '';
  repeat
    case CountColorTolerance(1319982, 355, 170, 435, 260, 4) of
      286: Result[2] := 'triangle';
      493: Result[2] := 'diamond';
      506: Result[2] := 'circle';
      729: Result[2] := 'square';
    end;
  until (not (Result[2] = '')) or (TimeFromMark(WatchTime) > 10000);
  PL_Message('[PL_GetKeys] Keys Found: '+Result[0]+' '+Result[1]+' '+Result[2]);
end;

(**
 * Author: DannyRS
 * Last modified 15/03/2013 By DannyRS
 * Description: Solves the Pillory Random Event
 *)
function PL_Solve: Boolean;
var
  i, t, keysolves, tmpCTS: Integer;
  tmpHM, tmpSM: Extended;
  lock: String;
  keys: TStringArray;
  PLError: Boolean;
begin
  Result := False;
  keysolves := 0;

  if (not LoggedIn) then Exit;

  t := GetSystemTime;

  MakeCompass('N');

  if (not PL_ClickCage) then Exit;

  repeat
    PLError := False;

    // open the lock screen if it's not already open
    if (not PL_LockScreen) then
      PL_ClickCage;

    tmpCTS := GetColorToleranceSpeed;
    GetToleranceSpeed2Modifiers(tmpHM, tmpSM);

    lock := PL_GetLock;
    keys := PL_GetKeys;

    for i := 0 to 2 do
      if (keys[i] = '') then
      begin
         PL_Message('[ERROR] [PL_GetKeys] Failed to get all keys');
         PLError := True;
         Break;
      end;

      if (PLError) then Continue;

      if (lock = '') then
      begin
         PL_Message('[ERROR] [PL_GetLock] Failed to get lock type');
         Continue;
      end;

      for i := 0 to 2 do
      if (keys[i] = lock) then
      begin
        PL_Message('[PL_Solve] Clicking key : ' + keys[i]);
        if PL_ClickKey(i) then
        begin
          keysolves := keysolves + 1;
          Wait(RandomRange(1500, 2000));
        end;
      end;

    Result := ((keysolves > 2) and (not PL_LockScreen));

  until (Result) or (GetSystemTime - t > 120000);

  SetColorToleranceSpeed(tmpCTS);
  SetToleranceSpeed2Modifiers(tmpHM, tmpSM);

  if (not Result) then
    if (GetSystemTime - t > 120000) then
      PL_Message('[ERROR] Pillory solver timed out');
end;
